group 'com.ds'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDir 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }

}

configurations{
    testPlugins{}
}

dependencies {
    //same as on jenkins 1.6.xx
    compile 'org.codehaus.groovy:groovy-all:1.8.9'
    //take a look at wrapper.properties there is aslo a version - conflicts?
    compile "org.jenkins-ci.plugins:job-dsl-core:1.51"

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
//    testCompile "cglib:cglib-nodep:2.2.2" //forSpock
    //jenkins itself
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.15'
    testCompile "org.jenkins-ci.main:jenkins-war:1.651.3"
    testCompile "org.jenkins-ci.main:jenkins-war:1.651.3:war-for-test@jar"

    //dsl
    testCompile 'org.jenkins-ci.plugins:job-dsl-core:1.51'
    testCompile 'org.jenkins-ci.plugins:job-dsl-core:1.51@jar'
    testCompile 'org.jenkins-ci.plugins:structs:1.2@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.13@jar'

//    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.31.4'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.19.0'


    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
        exclude(module: 'groovy')
    }

    compile('org.apache.ant:ant:1.9.7')

}

task resolveTestPlugins(type: org.gradle.api.tasks.Copy){
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast{
        def baseName=source.collect{ it.name[0..it.name.lastIndexOf('.')-1]}
        new File(destinationDir, 'index').setText(baseName.join('\n'),'UTF-8')

    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    exclude '**/XmlOutput.class'

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
